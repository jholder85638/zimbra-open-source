#!/usr/bin/perl -w
# 
# ***** BEGIN LICENSE BLOCK *****
# Zimbra Collaboration Suite Server
# Copyright (C) 2007, 2008, 2009, 2010, 2013, 2014, 2015, 2016 Synacor, Inc.
#
# This program is free software: you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free Software Foundation,
# version 2 of the License.
#
# This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
# without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
# See the GNU General Public License for more details.
# You should have received a copy of the GNU General Public License along with this program.
# If not, see <https://www.gnu.org/licenses/>.
# ***** END LICENSE BLOCK *****
# 

use strict;
use Getopt::Long;
use lib "/opt/zimbra/common/lib/perl5";
use Zimbra::Mon::Zmstat;
use Zimbra::Mon::Logger;

zmstatInit();

my ($CONSOLE, $LOGFH, $LOGFILE, $HEADING, $ROTATE_DEFER, $ROTATE_NOW);

sub getHeading() {
    return "timestamp, KBytes, requests";
}

sub getQueueStat() {
    my $out = qx(postqueue -p 2> /dev/null | tail -1);
    if (defined($out)) {
        chomp($out);
        if ($out =~ /(\d+) Kbytes in (\d+) Requests/) {
            return ($1, $2);
        }
    }
    return (0, 0);
}

sub usage() {
    print STDERR <<_USAGE_;
Usage: zmstat-mtaqueue [options]
Monitor MTA queue
-i, --interval: output a line every N seconds
-l, --log:      log file (default is /opt/zimbra/zmstat/mtaqueue.csv)
-c, --console:  output to stdout
--max-qsize:    max queue size at which to start backing off mointor interval (default 1000)
--backoff-interval: how many seconds to backoff monitor schedule if queue hits --max-qsize (default 30 seconds)
--max-interval: maximum monitor interval (default 600 seconds)

If logging to a file, rotation occurs when HUP signal is sent or when
date changes.  Current log is renamed to <dir>/YYYY-MM-DD/mtaqueue.csv
and a new file is created.
_USAGE_
    exit(1);
}

sub sighup {
    if (!$CONSOLE) {
        if (!$ROTATE_DEFER) {
            $LOGFH = rotateLogFile($LOGFH, $LOGFILE, $HEADING);
        } else {
            $ROTATE_NOW = 1;
        }
    }
}



#
# main
#

$| = 1; # Flush immediately

my $interval = getZmstatInterval();
my $max_qsize = 1000;
my $backoff_interval = 30;
my $max_interval = 600;
my $opts_good = GetOptions(
    'interval=i' => \$interval,
    'log=s' => \$LOGFILE,
    'console' => \$CONSOLE,
    'max-qsize=i' => \$max_qsize,
    'backoff-interval=i' => \$backoff_interval,
    'max-interval=i' => \$max_interval
    );
if (!$opts_good) {
    print STDERR "\n";
    usage();
}

if (!defined($LOGFILE) || $LOGFILE eq '') {
    $LOGFILE = getLogFilePath('mtaqueue.csv');
} elsif ($LOGFILE eq '-') {
    $CONSOLE = 1;
}
if ($CONSOLE) {
    $LOGFILE = '-';
}

createPidFile('zmstat-mtaqueue.pid');

$SIG{HUP} = \&sighup;

$HEADING = getHeading();
$LOGFH = openLogFile($LOGFILE, $HEADING);

my $curr_interval = $interval;
my $date = getDate();
my $t_last = waitUntilNiceRoundSecond($curr_interval);

while (1) {
    my ($kb, $msgs) = getQueueStat();

    my $tstamp = getTstamp();
    my $currDate = getDate();
    if ($currDate ne $date) {
        $LOGFH = rotateLogFile($LOGFH, $LOGFILE, $HEADING, $date);
        $date = $currDate;
    }

    # Don't allow rotation in signal handler while we're writing.
    $ROTATE_DEFER = 1;
    $LOGFH->print("$tstamp, $kb, $msgs\n");
    Zimbra::Mon::Logger::LogStats( "info", "zmstat mtaqueue.csv: ${HEADING}:: $tstamp, $kb, $msgs"); 
    $LOGFH->flush();
    $ROTATE_DEFER = 0;
    if ($ROTATE_NOW) {
        # Signal handler delegated rotation to main.
        $ROTATE_NOW = 0;
        $LOGFH = rotateLogFile($LOGFH, $LOGFILE, $HEADING);
    }

    if ($msgs > $max_qsize) {
        if ($curr_interval < $max_interval) {
            $curr_interval += $backoff_interval;
            if ($curr_interval > $max_interval) {
                $curr_interval = $max_interval;
            }
            print STDERR "$tstamp: Queue is too big! ($msgs msgs); Increasing monitor interval to $curr_interval seconds\n";
        } else {
            print STDERR "$tstamp: Queue is too big! ($msgs msgs); Already at maximum monitor interval of $curr_interval seconds\n";
        }
    } elsif ($curr_interval > $interval) {
        $curr_interval = $interval;
        print STDERR "$tstamp: Queue size is back within the maximum of $max_qsize; Restoring default monitor interval of $interval seconds\n";
    }

    sleep($curr_interval);
}
close($LOGFH);
